# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
epoch=1
pkgver=0.9.13
pkgrel=10
pkgdesc='VNC server for real X displays'
url='http://www.karlrunge.com/x11vnc/'
arch=('i686' 'x86_64')
license=('GPL2')
optdepends=('tk: GUI support'
            'net-tools: -auth guess'
            'xf86-video-dummy: Xdummy script')
depends=('openssl' 'libjpeg' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
source=("http://downloads.sourceforge.net/project/libvncserver/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'fix-buffer-overflows.patch'
        '0001-Fix-openssl-1.1.x-detection.patch'
        '0002-Support-openssl-1.1.0.patch'
        'service')
sha256sums=('f6829f2e629667a5284de62b080b13126a0736499fe47cdb447aedb07a59f13b'
            '21fe10bee45d6fcf3a41aba546cd1134a858dc78ee3f95f5137d50c22d566912'
            'f356009176a11a793fef4514b26468c04908c961e6be226a83b631b6df5a2fdc'
            'eaf92da28276273c1892631b063b0477148b9e2f7dd14645e82b4afbfe02c0c2'
            'cfb19d44e09e960e2fdb958c9258bccf23c2677715314985f7e819f1dcedb6e4')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../fix-buffer-overflows.patch
        patch -p1 -i ../0001-Fix-openssl-1.1.x-detection.patch
        patch -p1 -i ../0002-Support-openssl-1.1.0.patch
        autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install x11vnc/misc/{rx11vnc,Xdummy} "${pkgdir}"/usr/bin
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/x11vnc.service"
	rm -fr "${pkgdir}"/usr/include/rfb # provided by libvncserver
}
