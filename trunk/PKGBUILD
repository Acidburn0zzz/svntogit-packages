# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.7
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libpng' 'libjpeg' 'libxft' 'libxxf86vm'
	 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme'
	 'desktop-file-utils' 'lirc-utils')
makedepends=('libxt')
options=('!strip')
install=xine-ui.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz)
sha1sums=('ca82e09cf8245d038813bc4dbd8384ba79cbf197')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" desktopdir=/usr/share/applications install
}
