# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
pkgver=0.9.14
pkgrel=1
pkgdesc='VNC server for real X displays'
url='https://github.com/LibVNC/x11vnc/'
arch=('i686' 'x86_64')
license=('GPL2')
optdepends=('tk: GUI support'
            'net-tools: -auth guess'
            'xf86-video-dummy: Xdummy script')
depends=('libvncserver' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
source=("https://github.com/LibVNC/x11vnc/archive/${pkgver}.tar.gz"
        'service')
sha1sums=('f5903a5faade531ef6223d6740826a7729bc696c'
          '53e1ed7f84518a699a29607a03bee2321f9f9624')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	touch config.h.in
	aclocal; automake --add-missing; autoconf
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install misc/{rx11vnc,Xdummy} "${pkgdir}"/usr/bin
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/x11vnc.service"
}
