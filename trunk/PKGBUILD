# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=thunderbird-i18n
pkgver=17.0.2
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'be     "Belarusian"'
  'bg     "Bulgarian"'
  'bn-BD  "Bengali (Bangladesh)"'
  'br     "Breton"'
  'ca     "Catalan"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en-GB  "English (United Kingdom)"'
  'en-US  "English (United States)"'
  'es-AR  "Spanish (Argentina)"'
  'es-ES  "Spanish (Spain)"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'fy-NL  "Frisian (Netherlands)"'
  'ga-IE  "Irish"'
  'gd     "Gaelic"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'hy-AM  "Armenian"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'nb-NO  "Norwegian (Bokmal)"'
  'nl     "Dutch"'
  'nn-NO  "Norwegian (Nynorsk)"'
  'pa-IN  "Punjabi"'
  'pl     "Polish"'
  'pt-BR  "Brazilian Portuguese"'
  'pt-PT  "Portuguese"'
  'rm     "Romansh"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'si     "Sinhalese"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sq     "Albanian"'
  'sr     "Serbian"'
  'sv-SE  "Swedish"'
  'ta-LK  "Tamil (Sri Lanka)"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'vi     "Vietnamese"'
  'zh-CN  "Chinese (simplified)"'
  'zh-TW  "Chinese (traditional)"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}

md5sums=('46afc83b0330e886e13a90668d3ce24b'
         'e60a58f5d2db4c2250443050d9bc0723'
         'fdf3af74b22b4198cb0ecad6024d2b14'
         '0805550896db8e15a8fa489070788f48'
         'f83c1404b0553ea3f44189344714f6cc'
         'e502badb7fcc91b312c5eeb17cc722fd'
         '8b88a0ba24bfde3da1c8652fe75dc8cd'
         'bd82faab59437b0f33dccead7efc7eb8'
         'da57be7a409b5b8d406132f25db43f7a'
         'bf949e10311d05dc7c373dc915bdaa48'
         '539dfc1c23df958ecc6654c4d2170f6d'
         '9954e584899174c0f948f064d9c2b757'
         'ff91eebb4f906a1d171493d7ac186cd6'
         'c1e44b36e9e8e72c7e336787d17781ba'
         'c7213e8e055296c2c175673b1976fffa'
         'c917790fb8e15e3cfbb560b437a3372e'
         '8b110f770bf16b3fd6c32d8297fe060f'
         '74af1c253e47fc8199f72b3fde055e7e'
         '0a4b2ab366ef3dcd22ad51d22260199d'
         '275054e5e293e6bb518e17629b0ca751'
         '142a36b1f148df04384ecd379368c514'
         'bf8369a767b1c9c1fb6dca0d978c67fe'
         'f3f187b2a37372ccbd4dec7d6016f35d'
         'a78d3e09c33cc08928fe2255fbd996d1'
         'b18a056da828bc758540b493608a0c66'
         'd7cea5baebc89330fcbecaba9cc0c876'
         '7c761f8dff55f1d4bfe216b461f42e4b'
         '52a675528c8506eb8a21effd40c3b440'
         'fe48d12b0d8b4e5bfb3d5adb7bfcc4a5'
         'd519210f939cc5bd339a8387a5af4f23'
         'cfc3a7ca61f7f0703580d7a0393ef50c'
         '3714b92309d6e6b31963c342ab5b75de'
         '5cdfbe9c435c45712319e548e813564e'
         '3c66e0e2e0d5695a5c6c28281f77725a'
         '221b952f4209662fa8b05530edadb982'
         '93aadbe17b55931210b34407d8c04b46'
         '5ccfea7c46a94a5988bce3033f073c0c'
         'f65e9b5a3ccf7031c91e80a2c8536319'
         '15f4b7a17cc2dfd8d87467f7977b8e32'
         'e44115576042e8ab9eabb48db8bab7a4'
         'cead812402e95a1449e722a35448992b'
         '03b45e2eb5764070b930392013750aad'
         '6f043ec05521ef93fbbf3aaf7b623635'
         '5a4f890227addd059cc7accc4e0459c9'
         '43c778dcc6e108355875ac61ae93c155'
         'dcb7efd90f52aa606f62e9dde685eb8d'
         'ecbbf57a1b799181552daf0f3bf00c15'
         '6702d779aba24ea3a8ce33cf02992f68'
         '7baf493bc25d3f53bc8c1492675f3d34'
         '99dabbea411ea1d0f7c79c7b07892fd0'
         'd7099ef3a0aa5bdae4f84c01c28f4711'
         'fbdb4a0b2d76c6c616f8ac4a19820951'
         'a03a08f9da5bd3c91dc92a14a691c4fa'
         '4d2cc6197fb85415ba0d9d82fa0e4486'
         '0a868e535a67489fbd4c4aba19c4dc18')
