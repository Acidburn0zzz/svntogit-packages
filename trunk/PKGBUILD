# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=hyphen-fr
pkgver=2.0
pkgrel=2
pkgdesc="French hyphenation rules"
arch=('any')
url="http://www.dicollecte.org/"
license=('LGPL')
optdepends=('hyphen: offers hyphenation library functions')
source=("http://vesta.informatik.rwth-aachen.de/ftp/pub/mirror/OpenOffice/contrib/dictionaries/hyph_fr_FR_${pkgver/./-}.zip")
md5sums=('f6d9bd51a939df943612c3f5ac2a511c')

package() {
  cd "${srcdir}"
  install -dm755 ${pkgdir}/usr/share/hyphen
  install -m644 hyph_fr_FR.dic ${pkgdir}/usr/share/hyphen/hyph_fr_FR.dic

  pushd ${pkgdir}/usr/share/hyphen/
  fr_FR_aliases="fr_BE fr_CA fr_CH fr_LU"
  for lang in ${fr_FR_aliases}; do
        ln -s hyph_fr_FR.dic hyph_${lang}.dic
  done

  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename ${file}) .
    done
  popd
  
  # docs
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README_hyph_fr_FR.txt \
                ${pkgdir}/usr/share/doc/${pkgname}/README_hyph_fr_FR.txt
}
