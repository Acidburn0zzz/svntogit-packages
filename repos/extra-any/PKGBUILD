# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=mythes-fr
pkgver=2.2.2
pkgrel=1
pkgdesc="French thesaurus"
arch=(any)
url="http://www.dicollecte.org/"
license=('LGPL')
optdepends=('libmythes: offers thesaurus library functions')
source=(http://www.dicollecte.org/download/fr/thesaurus-v${pkgver}.zip)
md5sums=('4081b883b30a962b1f3087e55cb5a372')

package() {
  cd "${srcdir}"
  install -dm755 ${pkgdir}/usr/share/mythes
  install -m644 thes_fr.idx ${pkgdir}/usr/share/mythes/th_fr_FR.idx
  install -m644 thes_fr.dat ${pkgdir}/usr/share/mythes/th_fr_FR.dat

  pushd ${pkgdir}/usr/share/mythes/
  fr_FR_aliases="fr_BE fr_CA fr_CH fr_LU"
  for lang in ${fr_FR_aliases}; do
        ln -s th_fr_FR.idx "th_"${lang}".idx"
        ln -s th_fr_FR.dat "th_"${lang}".dat"
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/mythes/*; do
      ln -s /usr/share/mythes/$(basename ${file}) .
    done
  popd
  
  # docs
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README_thes_fr.txt \
                ${pkgdir}/usr/share/doc/${pkgname}/README_th_fr.txt
}
