# $Id: PKGBUILD,v 1.7 2009/04/05 09:41:36 firmicus Exp $
# Maintainer: Firmicus <francois.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: <AttilaHÂ©web.de>

pkgname=ttf-linux-libertine
pkgver=4.7.5
pkgrel=1
pkgdesc="Serif (Libertine) and Sans Serif (Biolinum) OpenType fonts with large Unicode coverage"
arch=('any')
license=('GPL' 'custom:OFL')
url="http://linuxlibertine.sourceforge.net/"
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
install=libertine.install
source=(http://downloads.sourceforge.net/sourceforge/linuxlibertine/LinLibertineFont-$pkgver-2.tgz)
md5sums=('789b4d8f0dd93b7234b7996e54a6a983')

build() {
  cd ${srcdir}/LinLibertine/Fonts
  for f in *.ttf; do
    name=$(echo $f | cut -f1 -d'-');
    mv $f $name.ttf;
  done
  for f in *.otf ; do
    name=$(echo $f | cut -f1 -d'-');
    mv $f $name.otf;
  done
}

package() {  
  cd ${srcdir}/LinLibertine/Fonts
  install -dm755 $pkgdir/usr/share/fonts/{OTF,TTF}
  install -m644 *.ttf $pkgdir/usr/share/fonts/TTF
  install -m644 *.otf $pkgdir/usr/share/fonts/OTF

  cd ${srcdir}/LinLibertine/
  install -Dm644 OFL.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENCE.txt $pkgdir/usr/share/licenses/$pkgname/README
}
