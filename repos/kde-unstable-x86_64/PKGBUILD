# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-apps-libs
pkgver=16.04.0
pkgrel=1
pkgdesc="KDE PIM mail related libraries"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(libkleo pimcommon grantleetheme)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('677cb160d12d5731a296b3f6a24bc270')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
