# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=user-manager
pkgver=5.8.4
pkgrel=2
pkgdesc='A simple system settings module to manage the users of your system'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(accountsservice kdelibs4support libpwquality)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-373276.patch::"https://cgit.kde.org/user-manager.git/patch/?id=f2c69db1")
sha256sums=('2da8fb75f2de0d15f167698ae168d1f9cbc1b94200b8d1cb2fc90349f1d95e54'
            'SKIP'
            'cbbbb0acb1ec504912b838a3a1f6f707c8ebfb5c572ffb2055fa10e260d8af4f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix adding new users (KDEBUG 373276)
  patch -p1 -i ../kdebug-373276.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
