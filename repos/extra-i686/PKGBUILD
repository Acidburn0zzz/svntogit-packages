# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-apps-libs
pkgver=17.04.0
pkgrel=1
pkgdesc="KDE PIM mail related libraries"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(libkleo pimcommon grantleetheme)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('76b43cede6b3de2971f6904a9058a9e30f4a74a060d6e67d717cfa8f58602f0a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
