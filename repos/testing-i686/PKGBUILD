# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.5
pkgrel=6
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft' 'libsm' 'libxxf86vm' 'ncurses' 'readline' 'xdg-utils')
makedepends=('lirc' 'libxt')
optdepends=('lirc')
options=('!emptydirs')
install=xine-ui.install
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.gz libpng14.patch)
md5sums=('e643cd1fcad4d98a5ae4eb877ce5087b' 'b533aa5a5847ecabad28cc15879e1966')
sha1sums=('4f452516319f31cda01d08ea2779f195933af0bb' '67db0d243e4fe482456db5efe174eaf4c6c50de6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../libpng14.patch || return 1
  #add missing constant (defined in xine.h from xine-lib cvs)
  echo '#define XINE_MSG_AUDIO_OUT_UNAVAILABLE 11' >> src/xitk/common.h || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-curses --with-x --enable-lirc --without-aalib || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -d "${pkgdir}/usr/share/applications"
  echo "Categories=Application;AudioVideo;" >> "${pkgdir}/usr/share/xine/desktop/xine.desktop" || return 1
  mv "${pkgdir}/usr/share/xine/desktop/xine.desktop" "${pkgdir}/usr/share/applications"
}
