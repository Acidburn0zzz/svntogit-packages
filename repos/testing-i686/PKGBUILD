# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.5
pkgrel=4
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://xinehq.de/"
depends=('xine-lib' 'curl>=7.16.2' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft' 'libsm' 'libxxf86vm' 'ncurses>=5.6-7')
makedepends=('lirc' 'libxt')
optdepends=('lirc')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.gz)
md5sums=('e643cd1fcad4d98a5ae4eb877ce5087b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  #add missing constant (defined in xine.h from xine-lib cvs)
  echo '#define XINE_MSG_AUDIO_OUT_UNAVAILABLE 11' >> src/xitk/common.h
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-curses --with-x --enable-lirc --without-aalib
  make || return 1
  make DESTDIR=${pkgdir} install
  install -d ${pkgdir}/usr/share/applications
  echo "Categories=Application;AudioVideo;" >> ${pkgdir}/usr/share/xine/desktop/xine.desktop
  mv ${pkgdir}/usr/share/xine/desktop/xine.desktop ${pkgdir}/usr/share/applications
}
