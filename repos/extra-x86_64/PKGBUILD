# $Id$
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
pkgver=0.9.11
pkgrel=1
pkgdesc='VNC server for real X displays'
arch=('i686' 'x86_64')
url='http://www.karlrunge.com/x11vnc/'
license=('GPL2')
depends=('openssl' 'libjpeg' 'zlib' 'libx11' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
optdepends=('tk: for GUI support')
backup=('etc/conf.d/x11vnc')
source=("http://downloads.sourceforge.net/libvncserver/${pkgname}-${pkgver}.tar.gz"
        'rename-pointer.patch'
	'x11vnc.rc'
	'x11vnc.conf')
sha1sums=('f24d747f3fbffc9740cfedbab0b39ea6da0c817d'
          '6e5d38676d1d76aa34ba7b1d11f9c5511c6d588d'
          '3abb3fcadff32662ab847a9e772cd23cfdfe3d1a'
          '11011cce06511e9ba61672a1c80bb63a3a187539')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # See http://bugs.gentoo.org/show_bug.cgi?id=334483
  patch -p1 < ../rename-pointer.patch

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -fr "${pkgdir}"/usr/include/rfb # provided by libvncserver

  install -D -m 755 "${srcdir}/x11vnc.rc" "${pkgdir}/etc/rc.d/x11vnc"
  install -D -m 644 "${srcdir}/x11vnc.conf" "${pkgdir}/etc/conf.d/x11vnc"
}
